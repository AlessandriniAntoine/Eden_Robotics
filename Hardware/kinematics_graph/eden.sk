## kinematics diagram for 4 dof arm (Eden robotics project)
## 
## Author : Alessandrini Antoine <alex@cimr.pub.ro>

def blue<>
input{lib/defaults.sk}
input{lib/objects.sk}

def J0 180
def J1 0
def J2 -90
def J3 180
def J4 0

def arrowarc_clockwise 
{
    sweep [style=thick]{215, rotate(1, [Z])} (1,0)
    def p1 (1,0) then rotate(20, [Z])
    def p2 (1,0) then rotate(0, [Z])
    line [style=thick, style=->] (p1)(p2)
}

def arrowarc_anticlockwise 
{
    sweep [style=thick]{215, rotate(1, [Z])} (1,0)
    def p1 (1,0) then rotate(210, [Z])
    def p2 (1,0) then rotate(210+30, [Z])
    line [style=thick, style=->] (p1)(p2)
}

% The main drawing
put {scale(0.5)}
{   
    # base
    put {scale(1)}
    {
        put{scale([8,8,0.5])}{box}
        put{scale(1.5) * translate([0,0,0.2])*rotate(J0,[Z])}
        {
            {coordsys}
            special |\path #1 node[left] {$Z_s$}
                           #2 node[left] {$X_s$}
                           #3 node[below] {$Y_s$};|(z)(x)(y)(o)
        }   

        put {translate([2,0,4.5]) * rotate(135, [Z])}{arrowarc_clockwise}
        special|\node[right] at #1 {$\theta_1$};|(-0.5,0,4)
    
        special|\draw [dashed, -] #1 -- node[below, sloped]{$h_0$} #2;|(0,0,0.2)(2,0,0.2)
        special|\draw [dashed, -] #1 -- node[above, sloped]{$L_0$} #2;|(2,0,0.2)(2,0,7)

        # link1
        put {translate([2,0,7]) * rotate(J1, [Z])}
        {
            put{scale([1,1,3])}{cylinder}

            {
                put{scale([2.5,2.5,4]) * rotate(180,[Y]) * rotate(90,[Z])}
                {
                    {coordsys}
                    special |\path #1 node[right] {$Z_1$}
                                #2 node[right] {$X_1$}
                                #3 node[right] {$Y_1$};|(z)(x)(y)(o)
                }
                put{translate([0,2,6]) * rotate(-90, [X])}
                {
                    put {translate([0,-3,-12.5]) * rotate(30, [Z])}{arrowarc_clockwise}
                    special|\node at #1 {$\theta_2$};|(-1,-4.1,-12)
                }
            }   

            special|\draw [dashed, -] #1 -- node[above, sloped]{$d_1$} #2;|(0,0,0)(0,-8,0)
            special|\draw [dashed, -] #1 -- node[above, sloped]{$L_1$} #2;|(0,-8,0)(0,-8,9)

            # link2
            put {translate([0,-8,9]) * rotate(-90, [X]) * rotate(J2+90, [Z])}
            {
                put{scale([1,1,3])}{cylinder}
                put{rotate(-90, [Z])}
                {
                    put {scale([3,3,4.5]) * rotate(-90,[Z]) * rotate(180,[Y])}
                    {
                        {coordsys}
                        special |\path #1 node[below] {$Z_2$}
                                    #2 node[above] {$X_2$}
                                    #3 node[right] {$Y_2$};|(z)(x)(y)(o)
                    }
                                   
                    put {translate([9,0,10]) * rotate(120, [Z])}{arrowarc_anticlockwise}
                    put{translate([9,0,2])}
                    {
                        special|\node at #1 {$\theta_3$};|(1.8,0,8)
                    }
                }

                special|\draw [dashed, -] #1 -- node[above, sloped]{$d_2$} #2;|(0,0,0)(0,0,6)
                special|\draw [dashed, -] #1 -- node[above, sloped]{$L_2$} #2;|(0,0,6)(0,-9,6)

                # link3
                put {translate([0,-9,6]) * rotate(J3-180, [Z]) * rotate(90, [Y]) * rotate(90, [Z])}
                {
                    put{rotate(90, [X]) * scale([1,1,3])}{cylinder}
                    put{scale([3,5.5,3]) * rotate(180,[Y])*rotate(-90,[X])}
                    {
                        {coordsys}
                        special |\path #1 node[right] {$Z_3$}
                                    #2 node[left] {$X_3$}
                                    #3 node[right] {$Y_3$};|(z)(x)(y)(o) 
                    } 
                    
                    special|\draw [dashed, -] #1 -- node[left, sloped]{$d_3$} #2;|(0,0,0)(0,-4,0)
                    special|\draw [dashed, -] #1 -- node[above, sloped]{$L_3$} #2;|(0,-4,0)(0,-4,16)

                    # link4
                    put {translate([0,-4,9.5]) * rotate(180+J4, [Z])}
                    {
                        put {translate([0,0,6.5]) * rotate(-90, [X]) * rotate(180, [Y])}
                        {
                            put{scale([1,1,3])}{cylinder}
                            put{scale([2.5,3,5]) * rotate(180,[Z])}
                            {
                                {coordsys}
                                special |\path #1 node[right] {$Z_4$}
                                               #2 node[right] {$X_4$}
                                               #3 node[below] {$Y_4$};|(z)(x)(y)(o)
                            }   

                            put {translate([0,0,4]) * rotate(-60, [Z])}{arrowarc_anticlockwise}
                            special|\node at #1 {$\theta_4$};|(0,-2,3)

                            special|\draw [dashed, -] #1 -- node[above=5pt, sloped]{$L_c$} #2;|(0,0,0)(-6,0,0)
                            special|\draw [dashed, -] #1 -- node[above, sloped]{$d_c$} #2;|(-6,0,0)(-6,0,-2)
                            special|\draw [dashed, -] #1 -- node[above, sloped]{$h_c$} #2;|(-6,0,-2)(-6,-4,-2)

                            # camera
                            put{translate([-6,-4,-2]) * rotate(180,[Z])}{
                                
                                put {scale([0.5,0.5,0.5])}{box}

                                put{scale([2,2,3]) * rotate(90,[X])}{
                                    {coordsys}
                                    special |\path #1 node[below] {$Y_c$}
                                                #2 node[right] {$X_c$}
                                                #3 node[below] {$Z_c$};|(y)(x)(z)(o)
                                }
                            }

                            special|\draw [dashed, -] #1 -- node[below, sloped]{$L_e$} #2;|(0,0,0)(0,-9,0)
                            special|\draw [dashed, -] #1 -- node[below, sloped]{$d_e$} #2;|(0,-9,0)(0,-9,-3)
                            special|\draw [dashed, -] #1 -- node[above, sloped]{$h_e$} #2;|(0,-9,-3)(-3,-9,-3)

                            # end effector
                            put{translate([-3,-9,-3]) * rotate(180,[Z])}{
                                
                                put{scale([0.5,0.5,1])}{box}
                                put{translate([0,0.5,-0.35]) * scale([0.5,0.5,0.1])}{box}
                                put{translate([0,0.5,0.35]) * scale([0.5,0.5,0.1])}{box}

                                put{scale([2,2,3]) * rotate(90,[X])}{
                                    {coordsys}
                                    special |\path #1 node[right] {$Y_e$}
                                                #2 node[right] {$X_e$}
                                                #3 node[below] {$Z_e$};|(y)(x)(z)(o)
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}



global 
{ 
    language tikz 
    camera rotate(0, (1,0,0)) * view((1.5,2,1),(0,0,0),[0,0,1]) * rotate(110, (0,0,1))
}


